plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = 'work.microhand'
version = '1.0-SNAPSHOT'

mainClassName = 'work.microhand.view.game.GomokuGame'

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.apache.derby/derby
    implementation 'org.apache.derby:derby:10.14.2.0'
    // https://mvnrepository.com/artifact/com.alibaba/druid
    implementation 'com.alibaba:druid:1.2.16'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'junit:junit:4.13.2'
}

def targetJavaVersion = 11
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

task packageJarAndDatabase(type: Zip) {
    dependsOn shadowJar
    from shadowJar.archivePath
    from 'src/main/resources'
    archiveFileName = 'GomokuGame.zip'
    destinationDir = file('build/libs')
}

test {
    useJUnitPlatform()
}